***************************************************************************
* Program: PICKLER
* Author : Ian McGowan
* Date   : 3/20/2014
* Checkin: $Id: $
* Comment: Create a self-unpacking archive for dict/data items
***************************************************************************
PROMPT ''
BLOB='PRINT "PICKLED by ':@LOGNAME:' on ':TIMEDATE():' in ':@ACCOUNT:'"'
BLOB<-1>='R=""'
BLOB<-1>=''
LOOP
    PRINT 'OUTPUT FILE:':
    INPUT OUTPUT.FILE
    IF OUTPUT.FILE='' OR OUTPUT.FILE='/' THEN STOP
    OPEN OUTPUT.FILE TO OUT.FVAR THEN
        EXIT
    END ELSE
        PRINT 'CANNOT OPEN ':OUTPUT.FILE
    END
REPEAT

LOOP
    PRINT 'OUTPUT ITEM:':
    INPUT OUTPUT.ITEM
    IF OUTPUT.ITEM='' OR OUTPUT.ITEM='/' THEN STOP
    READ DUMMY FROM OUT.FVAR, OUTPUT.ITEM THEN
        PRINT OUTPUT.ITEM:' exists.  Overwrite?  (Y/N):':
        INPUT OVERWRITE
        IF OVERWRITE='Y' THEN EXIT
    END ELSE
        EXIT
    END
REPEAT

LOOP
    PRINT '(DICT) FILE ITEM:':
    INPUT L
    IF L='' OR L='/' THEN EXIT
    IF FIELD(L,' ',1)='DICT' THEN
        FILE='DICT ':FIELD(L,' ',2)
        ITEM=FIELD(L,' ',3)
    END ELSE
        FILE=FIELD(L,' ',1)
        ITEM=FIELD(L,' ',2)
    END
    GOSUB ADD.ITEM
REPEAT
* Write out basic code that when run will recreate the record
BLOB<-1>=''
BLOB<-1>='OVERWRITE=0'
BLOB<-1>='FOR F=1 TO DCOUNT(R,@AM)'
BLOB<-1>='  REC=R<F>'
BLOB<-1>='  SWAP "#254#" WITH @AM IN REC'
BLOB<-1>='  SWAP "#134#" WITH "\" IN REC'
BLOB<-1>='  FILE=REC<1> ; DEL REC<1>'
BLOB<-1>='  ITEM=REC<1> ; DEL REC<1>'
BLOB<-1>='  PRINT FILE:" ":ITEM'
BLOB<-1>='  OPEN FILE TO FVAR ELSE STOP 201, FILE'
BLOB<-1>='  WFLAG=1'
BLOB<-1>='  READ DUMMY FROM FVAR, ITEM THEN'
BLOB<-1>='    IF NOT(OVERWRITE) THEN WFLAG=0 ; PRINT ITEM:" EXISTS, SKIPPED"'
BLOB<-1>='  END'
BLOB<-1>='  IF WFLAG THEN WRITE REC ON FVAR,ITEM'
BLOB<-1>='  CLOSE FVAR'
BLOB<-1>='NEXT F'
WRITE BLOB ON OUT.FVAR, OUTPUT.ITEM
STOP

ADD.ITEM:
    OPEN FILE TO FVAR ELSE
        PRINT 'Cannot open ':FILE
        RETURN
    END
    READ REC FROM FVAR, ITEM ELSE
        PRINT 'Cannot read ':FILE:' ':ITEM
        RETURN
    END
    INS ITEM BEFORE REC<1>
    INS FILE BEFORE REC<1>
    SWAP @AM WITH '#254#' IN REC
    IF INDEX(REC,'\',1) THEN
        SWAP '\' WITH '#134#' IN REC
    END
    BLOB<-1>='R<-1>=\':REC:'\'
    CLOSE FVAR
RETURN
