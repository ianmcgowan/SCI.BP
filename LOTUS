SUBROUTINE LOTUS(LIST,DESC,REPLY)
***************************************************************************
* Program: LOTUS
* Author : Ian McGowan
* Date   : 6/16/89
* Edited : 14:12:17 Jan 02 1998 By MCGOWAN
* Comment: Be careful about changing REPLY in the calling program
***************************************************************************
PROMPT '' ; VM = CHAR(253)
BINV = @(-13) ; EINV = @(-14)
BUND = @(-15) ; EUND = @(-16)
N = DCOUNT(LIST,VM) ; BOTTOM = @(0,23)

LOCATE REPLY IN LIST<1> SETTING KEY ELSE KEY=1

ECHO OFF
LOOP
   GOSUB BUILD.MENU
   CH = IN()
   BEGIN CASE
      CASE CH = '<' OR CH = ','
         KEY-=1 ; IF KEY < 1 THEN KEY=N
      CASE CH = '>' OR CH = '.' OR CH = ' '
         KEY+=1 ; IF KEY > N THEN KEY=1
      CASE SEQ(CH)=13
         CH=LIST<1,KEY> ; EXIT
      CASE 1
         CH = UPCASE(CH)
         LOCATE CH IN LIST<1> SETTING KEY THEN EXIT
   END CASE
REPEAT
REPLY = LIST<1,KEY>
ECHO ON
RETURN

BUILD.MENU:
   MENU = ''
   FOR I = 1 TO N
      PRMPT = DESC<1,I>
	  IF I = KEY THEN
		M = BINV:PRMPT:EINV
	  END ELSE
		M = BUND:PRMPT[1,1]:EUND:PRMPT[2,99]
      END
      MENU = MENU:M:'  '
   NEXT I
   PRINT BOTTOM:@(-4):MENU:
RETURN
