********************************************************************************
* Program: SCI.UCI.DEBUG
* Author : MCGOWJ01
* Date   : 2018-11-07
* Version: 2018-02-16
* Comment: Help troubleshoot UCI/XML issues
********************************************************************************
PROMPT ''
OPEN 'LS.MASTER,LS.REMOTE.LISTING' TO LS.REMOTE.LISTING ELSE STOP 201,'LS.REMOTE.LISTING'
OPEN 'EXTERN.PARAM.FILE,EXTERN.LAYOUT.FILE' TO EXTERN.LAYOUT.FILE ELSE STOP 201,'EXTERN.LAYOUT.FILE'
READ LAYOUT FROM EXTERN.LAYOUT.FILE, 'RAPPORT' ELSE STOP 'EXTERN.LAYOUT.FILE RAPPORT'
HEADER.REC=''
FIELD.REC=''
FOR L=1 TO DCOUNT(LAYOUT,@AM)
  R=LAYOUT<L>
  NAME=R<1,1>
  TYPE=R<1,5>
  *CRT NAME 'L#20':' ':TYPE
  IF TYPE='HEADER' THEN
    HEADER.REC<1,-1>=NAME ; GROUP=NAME
  END ELSE
    LOCATE GROUP IN HEADER.REC<1> SETTING POS THEN
      FIELD.REC<POS,-1>=NAME
    END ELSE
      STOP 'MISSING HEADER:':GROUP
    END
  END
NEXT L
*
LOOP
  CRT @(-1):'Enter Contract, LS.REMOTE.LISTING key, or X for raw XML:':
  INPUT KEY
  IF KEY='' OR KEY='/' THEN STOP
  XML=''
  BEGIN CASE
    CASE KEY='X'
      CRT 'Paste XML now:':
      INPUT XML
    CASE KEY[1,4]='xml_'
      READ R FROM LS.REMOTE.LISTING, KEY THEN
        GOSUB GET.XML
      END ELSE
        CRT KEY:' Not found'
      END
    CASE 1
      CALL SCI.CHECK.CONTRACT(RTN, KEY, OPTIONS)
      IF RTN = 'OK' THEN
        XML.LIST=''
        E=\SELECT LS.MASTER,LS.REMOTE.LISTING WITH CRR.CREATED.KEY = "\:KEY:\"\
        CRT E
        EXECUTE E
        LOOP
          READNEXT KEY ELSE EXIT
          XML.LIST<-1>=KEY
        REPEAT
        MAX.CTR=DCOUNT(XML.LIST,@AM)
        BEGIN CASE
          CASE MAX.CTR=1
            KEY=XML.LIST<1>
          CASE MAX.CTR > 1
            FOR F=1 TO DCOUNT(XML.LIST,@AM)
              CRT F, XML.LIST<F>
            NEXT F
            CRT 'Enter the key to dump:':
            INPUT N
            IF N='' OR N='/' THEN STOP
            KEY=XML.LIST<N>
          CASE 1
            CRT 'Contract not found'
        END CASE
        READ R FROM LS.REMOTE.LISTING, KEY THEN
          GOSUB GET.XML
        END ELSE
          CRT KEY:' Not found'
        END
      END ELSE
        CRT RTN
      END
  END CASE
  IF XML#'' THEN
    CONVERT '^' TO @AM IN XML
    MAX=DCOUNT(XML,@AM)
    X=1
    LOOP
      VALUE=XML<X>
      CRT VALUE
      LOCATE VALUE IN HEADER.REC<1> SETTING POS THEN
        GROUP=VALUE
        FIELD.LIST=FIELD.REC<POS>
        X += 1
      END
      FOR F=1 TO DCOUNT(FIELD.LIST<1>,@VM)
        VALUE=XML<X>
        CRT X 'R#4':' ':FIELD.LIST<1,F>'L#25':'=':VALUE
        X += 1
      NEXT F
      IF X>MAX THEN EXIT
    REPEAT
  END
  CRT 'Press ENTER:': ; INPUT AAA
REPEAT
STOP
*
GET.XML:
  LAST.LINE=DCOUNT(R,@AM)
  XML=R<LAST.LINE>
  S.TAG = \<UI_DATA>\
  E.TAG = \</UI_DATA>\
  PASS.XML = XML
  TAG.LEN = LEN(E.TAG)
  VALUE = ''; VALUE.STR = ''
  CALL API.50.PARSE.STRING(S.TAG,E.TAG,TAG.LEN,1,VALUE,VALUE.STR,PASS.XML)
  XML=VALUE
RETURN
*
