***************************************************************************
* Program: SCI.PARAM.MAINT
* Author : Ian McGowan
* Date   : 05/29/2018
* Version: 1.0
* Comment: Standard parameter maintenance for IL10 MV data
***************************************************************************
PROMPT ''
PROGRAM.NAME='SCI.PARAM.MAINT'
PROGRAM.DESC='Maintain Parameters'
OPEN '_HOLD_' TO F.HOLD ELSE STOP 201,'_HOLD_'
CALL GET.ARGS(@SENTENCE, ARGS, PARAMS, OPTIONS)
FILE.NAME=ARGS<1,1>
K.FILE=ARGS<1,2>
*
IF FILE.NAME='' THEN FILE.NAME='PARAMETER'
*
CALL FILE.OPEN(PROGRAM.NAME, FILE.NAME, F.FILE, 'STOP')
*
IF K.FILE='' THEN
  PRINT 'ENTER THE ID TO EDIT:':
  INPUT K.FILE
END
*
CALL IDS.READ(R.FILE, F.FILE,  K.FILE, 0, 0, BCI.ERROR)
IF BCI.ERROR # '' THEN STOP BCI.ERROR
*
LOOP
  PRINT DCOUNT(R.FILE,@AM):' fields in record'
  PRINT 'Enter E)dit, L)ist, S)ave or Q)uit:':
  INPUT OPT
  BEGIN CASE
    CASE OPT='L'
      GOSUB LIST
    CASE OPT='S'
      GOSUB SAVE
    CASE OPT='E'
      GOSUB EDIT
  END CASE
UNTIL OPT='Q' DO
REPEAT
STOP
*
LIST:
  PRINT @(-1):'FILE:':FILE.NAME:' ITEM:':K.FILE
  FOR F=1 TO DCOUNT(R.FILE,@AM)
    R=R.FILE<F>
    CONVERT @VM TO "|" IN R
    CONVERT @SVM TO "\" IN R
    PRINT F'R#3':' ':R
  NEXT F
  PRINT 'PRESS ENTER:':
  INPUT AAA
RETURN
*
SAVE:
  CALL IDS.WRITE(R.FILE, F.FILE, K.FILE, 0, 0)
  PRINT 'Saved.  Press ENTER to continue:':
  INPUT AAA
RETURN
*
EDIT:
  R=R.FILE
  PRINT 'Would you like to use J)oe or E)d to edit: ':
  INPUT EDITOR
  BEGIN CASE
    CASE EDITOR='E'
      WRITE R ON F.HOLD, K.FILE
      EXECUTE \ED _HOLD_ \:K.FILE
      READ R FROM F.HOLD, K.FILE ELSE R=''
    CASE EDITOR='J'
      * Convert the record to something that can be edited with joe
      CONVERT @VM TO "|" IN R
      CONVERT @SVM TO "\" IN R
      CONVERT @AM TO CHAR(10) IN R
      WRITE R ON F.HOLD, K.FILE
      EXECUTE \!/info/PROGRAMS/bin/joe _HOLD_/\:K.FILE
      * Now convert it all back
      READ R FROM F.HOLD, K.FILE ELSE R=''
      CONVERT CHAR(10) TO @AM IN R
      CONVERT "|" TO @VM IN R
      CONVERT "\" TO @SVM IN R
  END CASE
  IF R # R.FILE THEN
    PRINT 'Record changed, keep these changes? (Y/N):':
    INPUT YORN
    IF YORN='Y' THEN R.FILE=R
  END
  DELETE F.HOLD, K.FILE
RETURN
