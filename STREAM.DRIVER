********************************************************************************
* Program: STREAM.DRIVER
* Author : Ian McGowan
* Date   : 2017-09-29
* Version: 1.0
* Comment: Decide which streams need to be run
********************************************************************************
EXECUTE \COMO ON STREAM\
OPEN 'VOC' TO VOC ELSE STOP 201,'VOC'
OPEN 'PARAMETER' TO PARAMETER ELSE STOP 201,'PARAMETER'
MONTHEND.CODE=OCONV('00*00','TPARAMETER;X;15;15')
D=DATE()
DAY=OCONV(D,"DD")
DOW.NO=OCONV(D,"DW")
DAYS='MONDAY,TUESDAY,WEDNESDAY,THURSDAY,FRIDAY,SATURDAY,SUNDAY'
CONVERT ',' TO @AM IN DAYS
DOW=DAYS<DOW.NO>
PRINT 'STREAM.DRIVER:':OCONV(D,'D-YMD'):' ':DOW:' ':DAY
*
STREAM='DAILY'
GOSUB RUN.STREAM
*
STREAM=DAY
GOSUB RUN.STREAM
*
STREAM=DOW
GOSUB RUN.STREAM
*
IF MONTHEND.CODE = 5 THEN
  STREAM='MONTHLY'
  GOSUB RUN.STREAM
END
*
EXECUTE \COMO OFF\
STOP
*
RUN.STREAM:
  STREAM='STREAM.':STREAM
  PRINT 'Checking ':STREAM
  READV F1 FROM VOC, STREAM, 1 ELSE RETURN
  IF F1 # 'PA' THEN RETURN
  PRINT 'Running ':STREAM
  EXECUTE \PHANTOM \:STREAM
RETURN
*
