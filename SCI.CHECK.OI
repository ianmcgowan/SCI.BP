********************************************************************************
* Program: SCI.CHECK.OI
* Author : Ian McGowan
* Date   : 2018-12-11
* Version: 2018-12-11
* Comment: Check all active contracts to make sure open items are present
********************************************************************************
INCLUDE IL.BPI OI.CTD.INVOICE.DIM
INCLUDE IL.BPI OI.INVOICE.DIM
INCLUDE IL.BPI PAYMENT.TABLE.DIM
OPEN 'LS.MASTER,LS.OI.INVOICE' TO LS.OI.INVOICE ELSE STOP 201,'LS.OI.INVOICE'
OPEN 'LS.MASTER,LS.PYMT.TABLE' TO LS.PYMT.TABLE ELSE STOP 201,'LS.PYMT.TABLE'
OPEN 'LS.OI.CTD.INVOICE' TO LS.OI.CTD.INVOICE ELSE STOP 201,'LS.OI.CTD.INVOICE'
INVOICE.TO.DATE=OCONV("00*00","TPARAMETER,PARAMETER;X;10;10")
*
E=\SSELECT LS.MASTER WITH NUM.OF.ASSETS > "0"\
PRINT E
EXECUTE E
BAD.CTR=1
LOOP
  READNEXT ID ELSE EXIT
  MISSING.PYMTS=''
  MATREAD PAYMENT.TABLE FROM LS.PYMT.TABLE, ID THEN
    MATREAD OI.INVOICE FROM LS.OI.INVOICE, ID ELSE MAT OI.INVOICE=''
    FOR P=1 TO DCOUNT(PT.DATE<1>,@VM)
      PYMT.DATE=PT.DATE<1,P>
      PYMT.AMT=PT.AMOUNT<1,P>
      IF PYMT.AMT <= 0 THEN CONTINUE
      IF PYMT.DATE > INVOICE.TO.DATE THEN CONTINUE
      GOSUB FIND.OI
      IF NOT(FOUND) THEN
        MISSING.PYMTS<1,-1>=PYMT.DATE
        MISSING.PYMTS<2,-1>=PYMT.AMT
      END
    NEXT P
  END
  IF MISSING.PYMTS#'' THEN
    PRINT BAD.CTR'R#5':' ':ID:' ':
    FOR P=1 TO DCOUNT(MISSING.PYMTS<1>,@VM)
      PRINT SPACE(6):OCONV(MISSING.PYMTS<1,P>,'D4/'):' ':MISSING.PYMTS<2,P>
    NEXT P
    BAD.CTR+=1
  END
REPEAT
PRINT 'DONE'
STOP
*
FIND.OI:
  * Given the contract number, check to make sure a rental OI exists
  FOUND=0
  FOR OI.CTR=1 TO DCOUNT(OII.INVOICE.KEY<1>,@VM)
    OI.KEY=OII.INVOICE.KEY<1,OI.CTR>
    MATREAD OI.CTD.INVOICE FROM LS.OI.CTD.INVOICE, OI.KEY ELSE STOP 'MISSING OI:':OI.KEY
    IF OIC.DUE.DATE<1,1> = PYMT.DATE AND OIC.CHARGE.TYPE<1,1>='0001' THEN FOUND=1 ; EXIT
  NEXT OI.CTR
RETURN
*
