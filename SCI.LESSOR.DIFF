***************************************************************************
* Program: SCI.LESSOR.DIFF
* Author : Ian McGowan
* Date   : 2018-12-20
* Checkin: 2018-12-20
* Comment: Compare two or more lessor params side-by-side
***************************************************************************
PROMPT ''
$INCLUDE IL.BPI LESSOR.DIM
$INCLUDE IL.BPI ADDL.LESSOR.DIM
$INCLUDE IL.BPI LESSOR.USER.DIM
*
OPEN 'PARAMETER' TO PARAMETER ELSE STOP 201,'PARAMETER'
OPEN 'REV.ATB.LOG' TO REV.ATB.LOG ELSE STOP 201,'REV.ATB.LOG'
OPEN 'HELP.TEXT.USA' TO HELP.TEXT.USA ELSE STOP 201,'HELP.TEXT.USA'
OPEN 'IL.BPI' TO IL.BPI ELSE STOP 201,'IL.BPI'
GOSUB GET.BPI.FIELD.NAMES
*
EXECUTE \132\
LESSOR.LIST=''
LOOP
  CRT 'ENTER LESSOR:':
  INPUT LSR
  IF LSR='/' THEN STOP
  IF LSR='' THEN EXIT
  READ DUMMY FROM PARAMETER, LSR:'*00' THEN LESSOR.LIST<1,-1>=LSR ELSE CRT 'NOT FOUND'
REPEAT
*
LESSOR.VALUES=''
ADDL.LESSOR.VALUES=''
LESSOR.ADDRESS.VALUES=''
FOR L=1 TO DCOUNT(LESSOR.LIST<1>,@VM)
  LSR=LESSOR.LIST<1,L>
  READ R FROM PARAMETER, LSR THEN
    CONVERT @VM TO ',' IN R
    CONVERT @AM TO @VM IN R
    LESSOR.ADDRESS.VALUES<L>=R
  END
  READ R FROM PARAMETER, LSR:'*00' THEN
    CONVERT @VM TO ',' IN R
    CONVERT @AM TO @VM IN R
    LESSOR.VALUES<L>=R
  END
  READ R FROM PARAMETER, LSR:'*00B' THEN
    CONVERT @VM TO ',' IN R
    CONVERT @AM TO @VM IN R
    ADDL.LESSOR.VALUES<L>=R
  END
NEXT L
*
OUTPUT=''
LINE=''
CRT '   |':'PARAMETER''L#30':'|':
LINE<1,2>='CTR'
LINE<1,2>='PARAMETER'
FOR L=1 TO DCOUNT(LESSOR.LIST<1>,@VM)
  CRT LESSOR.LIST<1,L>'L#20':'|':
  LINE<1,-1>=LESSOR.LIST<1,L>
NEXT L
OUTPUT<-1>=LINE
CRT
FOR F=1 TO DCOUNT(LESSOR.ADDRESS.VALUES<1>,@VM)
  CRT F'R#3':'|':LESSOR.ADDRESS.BPI<1,F>'L#30':'|':
  LINE='' ; LINE<1,1>=F ; LINE<1,2>=LESSOR.ADDRESS.BPI<1,F>
  FOR L=1 TO DCOUNT(LESSOR.LIST<1>,@VM)
    CRT LESSOR.ADDRESS.VALUES<L,F>'L#20':'|':
    LINE<1,-1>=LESSOR.ADDRESS.VALUES<L,F>
  NEXT L
  OUTPUT<-1>=LINE
  CRT
NEXT F
*
FOR F=1 TO DCOUNT(LESSOR.VALUES<1>,@VM)
  CRT F'R#3':'|':LESSOR.BPI<1,F>'L#30':'|':
  LINE='' ; LINE<1,1>=F ; LINE<1,2>=LESSOR.BPI<1,F>
  FOR L=1 TO DCOUNT(LESSOR.LIST<1>,@VM)
    CRT LESSOR.VALUES<L,F>'L#20':'|':
    IF F=58 OR F=97 THEN
      LINE<1,-1>=LESSOR.VALUES<L,F>'L#20' ;* Don't need 1000 change log pointers, or prev. fiscal dates
    END ELSE
      LINE<1,-1>=LESSOR.VALUES<L,F>
    END
  NEXT L
  OUTPUT<-1>=LINE
  CRT
NEXT F
*
FOR F=1 TO DCOUNT(ADDL.LESSOR.VALUES<1>,@VM)
  CRT F'R#3':'|':ADDL.LESSOR.BPI<1,F>'L#30':'|':
  LINE='' ; LINE<1,1>=F ; LINE<1,2>=ADDL.LESSOR.BPI<1,F>
  FOR L=1 TO DCOUNT(LESSOR.LIST<1>,@VM)
    CRT ADDL.LESSOR.VALUES<L,F>'L#20':'|':
    LINE<1,-1>=ADDL.LESSOR.VALUES<L,F>
  NEXT L
  OUTPUT<-1>=LINE
  CRT
NEXT F
*
SWAP @VM WITH CHAR(9) IN OUTPUT
SWAP @AM WITH CHAR(13):CHAR(10) IN OUTPUT
OSWRITE OUTPUT ON '/tmp/lessor_compare.xls'
CONVERT @VM TO ',' IN LESSOR.LIST
SOURCE='PFA Lessor compare for:':LESSOR.LIST
SUBJECT="Lessor compare"
TO.USER=''
FROM.USER=''
OPTIONS=""
CC.USER=""
ATTACH='/tmp/lessor_compare.xls'
CALL SCI.MAIL.SUB(TO.USER, FROM.USER, CC.USER, SOURCE, ATTACH, SUBJECT, OPTIONS)
*
STOP
*
GET.BPI.FIELD.NAMES:
  LESSOR.ADDRESS.BPI=''
  READ BPI FROM IL.BPI, 'LS.ADDRESS' ELSE ABORT
  FOR F=1 TO DCOUNT(BPI,@AM)
    L=BPI<F>
    IF FIELD(L,' ',1) = 'EQUATE' THEN
      ATB=FIELD(L,' ',2)
      FLD=FIELD(FIELD(L,'(',2),')',1)
      LESSOR.ADDRESS.BPI<1,FLD>=ATB
    END
  NEXT F
  LESSOR.BPI=''
  READ BPI FROM IL.BPI, 'LESSOR' ELSE ABORT
  FOR F=1 TO DCOUNT(BPI,@AM)
    L=BPI<F>
    IF FIELD(L,' ',1) = 'EQUATE' THEN
      ATB=FIELD(L,' ',2)
      FLD=FIELD(FIELD(L,'(',2),')',1)
      LESSOR.BPI<1,FLD>=ATB
    END
  NEXT F
  ADDL.LESSOR.BPI=''
  READ BPI FROM IL.BPI, 'ADDL.LESSOR' ELSE ABORT
  FOR F=1 TO DCOUNT(BPI,@AM)
    L=BPI<F>
    IF FIELD(L,' ',1) = 'EQUATE' THEN
      ATB=FIELD(L,' ',2)
      FLD=FIELD(FIELD(L,'(',2),')',1)
      ADDL.LESSOR.BPI<1,FLD>=ATB
    END
  NEXT F
RETURN
*
