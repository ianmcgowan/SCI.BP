SUBROUTINE CVS.CHECKIN(RTN, FILE, ITEM, OPTIONS)
***************************************************************************
* Program: CVS.CHECKIN
* Author : $Author: mcgowi96 $
* Date   : 11/08/2002
* Edited : $Id: CVS.CHECKIN 17455 2015-03-17 00:00:49Z mcgowi96 $
* Comment: Check a file into version control
***************************************************************************
IF OPTIONS<1>='AUTO' THEN AUTO=1 ELSE AUTO=0

PROMPT ''
RTN=''
CALL CVS.GETDIR(DIR,FILE)

* Decide if we need to add or commit
CALL CVS.STATUS(STATUS.RTN,FILE,ITEM,'')
STATUS=STATUS.RTN<1>
BEGIN CASE
   CASE STATUS='NOTINCVS'
      GOSUB ADD.IT; SLEEP 3; GOSUB COMMIT.IT
   CASE STATUS='UPTODATE'
      IF NOT(AUTO) THEN PRINT 'Already up-to-date'
   CASE STATUS='MODIFIED'
      GOSUB COMMIT.IT
   CASE 1
      PRINT 'Weird status, aborting-':STATUS
END CASE
RETURN

ADD.IT:
    PRINT "adding ":DIR:" ":ITEM:" to subversion "
    E=\!cd \:DIR:\;svn add \:ITEM
    IF NOT(AUTO) THEN PRINT E
    EXECUTE E CAPTURING OUTPUT
    IF NOT(AUTO) THEN
       I=DCOUNT(OUTPUT,@AM)
       FOR F=1 TO I; PRINT OUTPUT<F>; NEXT F
    END
RETURN

COMMIT.IT:
    IF AUTO THEN
       MSG="Auto+Checkin"
       E=\!cd \:DIR:\;svn commit -m "\:MSG:\" \:ITEM
       EXECUTE E
    END ELSE
       E=\!cd \:DIR:\;svn commit \:ITEM
       PRINT E
       EXECUTE E
    END
RETURN
