SUBROUTINE CVS.DIFF(RTN,FILE,ITEM,OPTIONS)
***************************************************************************
* Program: CVS.DIFF
* Author : $mcgowan
* Date   : 11/08/2002
* Edited : 17:32:31 Nov 08 2002 By MCGOWAN
* Comment: Do a diff, basically same as checkout!
***************************************************************************
RTN=''
DEBUG = 0
CALL CVS.STATUS(STATUS.RTN,FILE,ITEM,'')
STATUS=STATUS.RTN<1>
FLAGS=STATUS.RTN<2>

IF DEBUG THEN PRINT "(CVS.DIFF) STATUS=":STATUS
IF STATUS='NOTINCVS' THEN
   IF OPTIONS<1>='SHOW' THEN
      PRINT ITEM:' not in svn'
   END
   RTN=-1
   RETURN
END

CALL CVS.GETDIR(DIR,FILE)

* If "diff is not working", you probably have what svn calls
* "mixed revision working copies".  The short answer is to
* run svn update or /N on that file.  See this link for info:
* http://svnbook.red-bean.com/en/1.7/svn.basic.in-action.html#svn.basic.in-action.mixedrevs
E=\!cd \:DIR:\;svn diff -r HEAD \:ITEM
EXECUTE E CAPTURING RTN
IF RTN = '' THEN
   IF OPTIONS<1>='SHOW' THEN
      PRINT 'Working version matches svn'
   END
END ELSE
   IF OPTIONS<1>='SHOW' THEN
      I=DCOUNT(RTN,@AM)
      FOR F=1 TO I; PRINT RTN<F>; NEXT F
   END
END
RETURN
