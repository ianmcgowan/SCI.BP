SUBROUTINE CVS.CHECKOUT(RTN, FILE, ITEM, OPTIONS)
***************************************************************************
* Program: CVS.CHECKOUT
* Author : Ian McGowan
* Date   : 11/08/2002
* Edited : $Id: CVS.CHECKOUT 16396 2011-05-04 23:42:17Z stiffd01 $
* Comment: Check to see if working file matches svn version, warn
***************************************************************************
PROMPT ':'
RTN=''
DEBUG = 0
CALL CVS.GETDIR(DIR,FILE)
CALL CVS.STATUS(STAT.RTN,FILE,ITEM,'')
STATUS=STAT.RTN<1>

IF DEBUG THEN PRINT "(CVS.CHECKOUT) STATUS=":STATUS
BEGIN CASE
    CASE STATUS='NOTINCVS'
        PRINT "New program"
    CASE STATUS='UPTODATE'
        PRINT "Working version matches CVS"
    CASE STATUS='MODIFIED'
        GOSUB UPDATE
    CASE 1
        PRINT "Unknown svn status:":STAT.RTN
END CASE
IF DEBUG THEN PRINT "(CVS.CHECKOUT) STATUS=":STATUS
RETURN

UPDATE:
    CALL CVS.DIFF(DIFF.RTN,FILE,ITEM,'')
    IF DIFF.RTN = '' THEN
        PRINT "CVS status says an update is required.  This may be "
        PRINT "because diff ignores changes in whitespace or CVS"
        PRINT "comments, or because the versions are different"
        PRINT
        PRINT "An update may clear this up, proceed? ":
        INPUT YORN
        IF YORN = "Y" THEN
            E=\!mv \:DIR:\/\:ITEM:\ /tmp\
            EXECUTE E
            E=\!cd \:DIR:\;svn update \:ITEM
            EXECUTE E
        END
    END ELSE
        PRINT "Working version does not match svn version"
        I=DCOUNT(DIFF.RTN,@AM)
        FOR F=1 TO I; PRINT "Diff:":DIFF.RTN<F>; NEXT F
        PRINT
        PRINT "Would you like to "
        PRINT "   U) pdate this item from svn (losing changes), or"
        PRINT "   C) heckin the changes, or"
        PRINT " RTN) Do nothing"
        INPUT ACTION
        BEGIN CASE
            CASE ACTION='U'
                PRINT 'Are you sure you want to overwrite the working copy? ':
                INPUT YORN
                IF YORN='Y' THEN
                    E=\!mv \:DIR:\/\:ITEM:\ /tmp\
                    EXECUTE E
                    E=\!cd \:DIR:\;svn update \:ITEM
                    EXECUTE E
                END
            CASE ACTION='C'
                CI.OPTIONS=''
                CALL CVS.CHECKIN(CI.RTN,FILE,ITEM,CI.OPTIONS)
            CASE 1
                PRINT 'No action taken'
        END CASE
    END
RETURN
