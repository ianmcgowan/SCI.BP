PROGRAM SCI.CHECK.LOCKS
PRINT @(-1)
*
OPEN 'PARAMETER' TO PARAMETER ELSE STOP 201,'PARAMETER'
*
* Check to make sure an NJS is setup...
NJS.SETUP=1
PRINT 'CHECKING NJS SETUP'
READ SETUP FROM PARAMETER, 'TONIGHTS.SETUP' ELSE
  PRINT 'NO NJS SETUP'
  NJS.SETUP=0
END
*
* Check for processing flags
READV PFLAGS FROM PARAMETER, '00*00', 16 ELSE PFLAGS=0
PFLAG.CTR=SUM(PFLAGS)
IF PFLAG.CTR#0 THEN
  PRINT 'PROCESSING FLAGS FOUND:':PFLAG.CTR
END
*  
* Now check for locks in all subfiles
*
OPEN 'DICT','DB.RECORD.LOCKS' TO D.DB.RECORD.LOCKS ELSE
  PRINT 'RESPONSE: CANNOT OPEN DICT DB.RECORD.LOCKS'
  STOP 201,'DICT DB.RECORD.LOCKS'
END
*
* Create a list of subfiles...
LOCKED.FILES=0
SELECT D.DB.RECORD.LOCKS TO L1
LOOP
  READNEXT ID FROM L1 ELSE EXIT
  IF ID[1,1]='@' THEN
    FNAME=ID[2,99]
    GOSUB CHECK.FILE
  END
REPEAT
PRINT 'RESPONSE: RECORD LOCKS/PROCESSING FLAGS:':
IF NJS.SETUP=1 AND LOCKED.FILES=0 AND PFLAG.CTR=0 THEN PRINT 'OK' ELSE PRINT 'BAD'
PRINT 'Press ENTER to return to the menu':
INPUT AAA
STOP

CHECK.FILE:
  FNAME='DB.RECORD.LOCKS,':FNAME
  OPEN FNAME TO FPTR ELSE RETURN ;* IGNORE ERRORS FROM @ID
  CTR=0
  SELECT FPTR TO L2
  LOOP
    READNEXT DUMMY.ID FROM L2 ELSE EXIT
    CTR+=1
  REPEAT
  IF CTR>0 THEN LOCKED.FILES+=1 ; PRINT FNAME:' HAS ':CTR:' LOCKS!'
  CLOSE FPTR
RETURN
